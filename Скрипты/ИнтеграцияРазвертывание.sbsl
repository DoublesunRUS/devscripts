#!/usr/bin/executor

/*******************************************************************************
 * Copyright (c) 2020 Alexander Kapralov and Contributors
 * This program and the accompanying materials are made available under
 * the terms of the BSD 3-Clause License which is available at
 * https://spdx.org/licenses/BSD-3-Clause.html#licenseText
 *
 * SPDX-License-Identifier: BSD-3-Clause
 *
 * Contributors:
 *    
 *
 ******************************************************************************/

конст ОТЛАДКА = Истина


структура ОписаниеУправляющегоФайла
    знч путьКРабочейОбласти: Строка
    знч путьКФайламКонфигурации: Строка
    знч путьКИБ: Строка

    пер конфигурацияЕДТЗагружена: Булево
    пер конфигурация1СВыгружена: Булево
    пер ИБСоздана: Булево

    конструктор(путьКРабочейОбласти, путьКФайламКонфигурации, путьКИБ)
;


метод Инициализировать(): Строка
    знч имяУправляющегоФайла = Файлы.СоздатьВременныйФайл("cicd", ".json", Ложь).Путь

    если ОТЛАДКА
        Консоль.Записать("Управляющий файл: " + имяУправляющегоФайла)
        Консоль.Записать("")
    ;

    знч временныйПутьКРабочейОбласти = Файлы.СоздатьВременныйКаталог("cicd_ws", Ложь).Путь
    знч временныйПутьКФайламКонфигурации = Файлы.СоздатьВременныйКаталог("cicd_1c_xml", Ложь).Путь
    знч временныйПутьКИБ = Файлы.СоздатьВременныйКаталог("cicd_1cd", Ложь).Путь
    если ОТЛАДКА
        Консоль.Записать("Рабочая область: " + временныйПутьКРабочейОбласти)
        Консоль.Записать("Файлы конфигурации: " + временныйПутьКФайламКонфигурации)
        Консоль.Записать("ИБ: " + временныйПутьКИБ)
    ;

    знч содержимоеФайла = новый ОписаниеУправляющегоФайла(временныйПутьКРабочейОбласти, временныйПутьКФайламКонфигурации,
        временныйПутьКИБ)

    содержимоеФайла.конфигурацияЕДТЗагружена = Истина
    содержимоеФайла.конфигурация1СВыгружена = не Файлы.КаталогПустой(временныйПутьКРабочейОбласти) и не Файлы.КаталогПустой(
        временныйПутьКФайламКонфигурации)
    содержимоеФайла.ИБСоздана = не Файлы.КаталогПустой(временныйПутьКИБ)

    ЗаписатьУправляющийФайл(содержимоеФайла, имяУправляющегоФайла)

    если ОТЛАДКА
        Консоль.Записать("")
    ;

    возврат имяУправляющегоФайла
;

метод ПрочитатьУправляющийФайл(имяУправляющегоФайла: Строка): ОписаниеУправляющегоФайла
    пер путьКРабочейОбласти: Строка
    пер путьКФайламКонфигурации: Строка
    пер путьКИБ: Строка
    пер конфигурацияЕДТЗагружена: Булево
    пер конфигурация1СВыгружена: Булево
    пер ИБСоздана: Булево

    знч управляющийФайл = новый Файл(имяУправляющегоФайла)
    знч потокЧтения = управляющийФайл.ОткрытьПотокЧтения()
    знч чтение = новый ЧтениеJson(потокЧтения)
    пока чтение.Следующий()
        если чтение.ВидУзла != ВидУзлаJson.ИмяСвойства
        продолжить
        ;

        выбор чтение.Значение
        когда "ПутьКРабочейОбласти"
            чтение.Следующий()
            путьКРабочейОбласти = чтение.Значение

        когда "ПутьКФайламКонфигурации"
            чтение.Следующий()
            путьКФайламКонфигурации = чтение.Значение

        когда "ПутьКИБ"
            чтение.Следующий()
            путьКИБ = чтение.Значение

        когда "КонфигурацияЕДТЗагружена"
            чтение.Следующий()
            конфигурацияЕДТЗагружена = чтение.ПрочитатьСодержимоеКакБулево()

        когда "Конфигурация1СВыгружена"
            чтение.Следующий()
            конфигурация1СВыгружена = чтение.ПрочитатьСодержимоеКакБулево()

        когда "ИБСоздана"
            чтение.Следующий()
            ИБСоздана = чтение.ПрочитатьСодержимоеКакБулево()

        иначе
        ;
    ;

    потокЧтения.Закрыть()

    знч содержимоеФайла = новый ОписаниеУправляющегоФайла(путьКРабочейОбласти, путьКФайламКонфигурации, путьКИБ)

    возврат содержимоеФайла
;

метод ЗаписатьУправляющийФайл(содержимоеФайла: ОписаниеУправляющегоФайла, имяУправляющегоФайла: Строка)
    знч управляющийФайл = новый Файл(имяУправляющегоФайла)
    знч потокЗаписи = управляющийФайл.ОткрытьПотокЗаписи()
    знч запись = новый ЗаписьJson(потокЗаписи)

    запись.ЗаписатьНачалоОбъекта()
    
    запись.Записать("ПутьКРабочейОбласти", содержимоеФайла.путьКРабочейОбласти)
    запись.Записать("ПутьКФайламКонфигурации", содержимоеФайла.путьКФайламКонфигурации)
    запись.Записать("ПутьКИБ", содержимоеФайла.путьКИБ)
    
    запись.Записать("КонфигурацияЕДТЗагружена", содержимоеФайла.конфигурацияЕДТЗагружена)
    запись.Записать("Конфигурация1СВыгружена", содержимоеФайла.конфигурация1СВыгружена)
    запись.Записать("ИБСоздана", содержимоеФайла.ИБСоздана)
    
    запись.ЗаписатьКонецОбъекта()

    потокЗаписи.Закрыть()
;

метод Завершить(имяУправляющегоФайла: Строка)
    знч содержимоеФайла = ПрочитатьУправляющийФайл(имяУправляющегоФайла)

    Файлы.Удалить(содержимоеФайла.путьКРабочейОбласти)
    Файлы.Удалить(содержимоеФайла.путьКФайламКонфигурации)
    Файлы.Удалить(содержимоеФайла.путьКИБ)
    
    Файлы.Удалить(имяУправляющегоФайла)
;

метод СкачатьКоммитИзРепозитория()
;

метод ПутьКПлатформе(версияПлатформы: Строка): Строка
    знч максимальныеПлатформы = МаксимальныеПлатформы()

    пер путьКПлатформе = максимальныеПлатформы.Получить(версияПлатформы)
    если путьКПлатформе == Неопределено
        знч текстОшибки = Строки.Шаблон("Не найдена установленная платформа $0", версияПлатформы)
        выбросить новый ИсключениеНедопустимоеСостояние(текстОшибки)
    ;
    путьКПлатформе = путьКПлатформе + Файлы.СимволРазделителя + "bin" + Файлы.СимволРазделителя + "1cv8"

    возврат путьКПлатформе
;

метод СобратьИзИсходниковФайлКонфигурации(путьККонфигурацииЕДТ: Строка, путьКФайлуCF: Строка, версияПлатформы: Строка)
    пер кодВозврата: Число

    знч имяУправляющегоФайла = Инициализировать()
    знч содержимоеФайла = ПрочитатьУправляющийФайл(имяУправляющегоФайла)

    знч путьКПлатформе = ПутьКПлатформе(версияПлатформы)

    если не содержимоеФайла.конфигурация1СВыгружена
        Консоль.Записать("Выгрузка конфигурации ЕДТ в формат 1С")
        кодВозврата = Ring_EDT_Export(путьККонфигурацииЕДТ, содержимоеФайла.путьКФайламКонфигурации, содержимоеФайла.путьКРабочейОбласти)
        содержимоеФайла.конфигурация1СВыгружена = кодВозврата == 0
        ЗаписатьУправляющийФайл(содержимоеФайла, имяУправляющегоФайла)
        если кодВозврата != 0
            Завершить(имяУправляющегоФайла)
        возврат
    ;
    ;

    если не содержимоеФайла.ИБСоздана
        Консоль.Записать("Создание пустой ИБ")
        кодВозврата = E1c_CreateInfobase(путьКПлатформе, содержимоеФайла.путьКИБ)
        если кодВозврата != 0
            содержимоеФайла.ИБСоздана = Ложь
            ЗаписатьУправляющийФайл(содержимоеФайла, имяУправляющегоФайла)
            Завершить(имяУправляющегоФайла)
        возврат
        ;

        Консоль.Записать("Загрузка конфигурации из XML в ИБ")
        кодВозврата = E1c_LoadConfigFromFiles(путьКПлатформе, содержимоеФайла.путьКИБ, содержимоеФайла.путьКФайламКонфигурации)
        содержимоеФайла.ИБСоздана = кодВозврата == 0
        ЗаписатьУправляющийФайл(содержимоеФайла, имяУправляющегоФайла)
        если кодВозврата != 0
            Завершить(имяУправляющегоФайла)
        возврат
    ;
    ;

    Консоль.Записать("Сохранение CF")
    кодВозврата = E1c_DumpCfg(путьКПлатформе, содержимоеФайла.путьКИБ, путьКФайлуCF)
    если кодВозврата != 0
        Завершить(имяУправляющегоФайла)
    возврат
    ;

    Завершить(имяУправляющегоФайла)
    Консоль.Записать("CF файл собран")
;

метод СобратьИзИсходниковФайлРасширения()
;

метод Ring_EDT_PlatformVersions(версияЕДТ: Строка = ""): Число
    пер имяКоманды = ""
    пер параметрыПроцесса = новый Массив()

    знч версияОС = ИспользуемаяОС()
    выбор версияОС
    когда ОперационныеСистемы.Windows
        имяКоманды = "cmd.exe"
        параметрыПроцесса.Добавить("/c")
    иначе
        имяКоманды = "zsh"
        параметрыПроцесса.Добавить("-c")
    ;

    параметрыПроцесса.Добавить("ring")
    параметрыПроцесса.Добавить("-l")
    параметрыПроцесса.Добавить("error")
    если версияЕДТ.Пусто()
        параметрыПроцесса.Добавить("edt")
    иначе
        параметрыПроцесса.Добавить("edt@" + версияЕДТ)
    ;
    параметрыПроцесса.Добавить("platform-versions")

    возврат ВыполнитьКомандуСистемы(имяКоманды, параметрыПроцесса, Истина)
;

метод Ring_EDT_Export(путьККонфигурацииЕДТ: Строка, путьККонфигурацииПлатформы: Строка, путьКРабочейОбласти: Строка,
    версияЕДТ: Строка = ""): Число
    пер имяКоманды = ""
    пер параметрыПроцесса = новый Массив()

    знч версияОС = ИспользуемаяОС()
    выбор версияОС
    когда ОперационныеСистемы.Windows
        имяКоманды = "cmd.exe"
        параметрыПроцесса.Добавить("/c")
    иначе
        имяКоманды = "zsh"
        параметрыПроцесса.Добавить("-c")
    ;

    параметрыПроцесса.Добавить("ring")
    параметрыПроцесса.Добавить("-l")
    параметрыПроцесса.Добавить("error")
    если версияЕДТ.Пусто()
        параметрыПроцесса.Добавить("edt")
    иначе
        параметрыПроцесса.Добавить("edt@" + версияЕДТ)
    ;
    параметрыПроцесса.Добавить("workspace")
    параметрыПроцесса.Добавить("export")
    параметрыПроцесса.Добавить("--project")
    параметрыПроцесса.Добавить(путьККонфигурацииЕДТ)
    параметрыПроцесса.Добавить("--configuration-files")
    параметрыПроцесса.Добавить(путьККонфигурацииПлатформы)
    параметрыПроцесса.Добавить("--workspace-location")
    параметрыПроцесса.Добавить(путьКРабочейОбласти)

    возврат ВыполнитьКомандуСистемы(имяКоманды, параметрыПроцесса, Ложь)
;

метод E1c_CreateInfobase(имяФайлаПлатформы: Строка, путьКИБ: Строка): Число
    пер параметрыПроцесса = новый Массив()
    параметрыПроцесса.Добавить("CREATEINFOBASE")
    параметрыПроцесса.Добавить("File=" + путьКИБ)

    возврат ВыполнитьКомандуСистемы(имяФайлаПлатформы, параметрыПроцесса)
;

метод E1c_LoadConfigFromFiles(имяФайлаПлатформы: Строка, путьКИБ: Строка, путьККонфигурацииПлатформы: Строка): Число
    пер параметрыПроцесса = новый Массив()
    параметрыПроцесса.Добавить("DESIGNER")
    параметрыПроцесса.Добавить("/F")
    параметрыПроцесса.Добавить(путьКИБ)
    параметрыПроцесса.Добавить("/LoadConfigFromFiles")
    параметрыПроцесса.Добавить(путьККонфигурацииПлатформы)
    параметрыПроцесса.Добавить("/UpdateDBCfg")

    возврат ВыполнитьКомандуСистемы(имяФайлаПлатформы, параметрыПроцесса)
;

метод E1c_DumpCfg(имяФайлаПлатформы: Строка, путьКИБ: Строка, путьКФайлуCF: Строка): Число
    пер параметрыПроцесса = новый Массив()
    параметрыПроцесса.Добавить("DESIGNER")
    параметрыПроцесса.Добавить("/F")
    параметрыПроцесса.Добавить(путьКИБ)
    параметрыПроцесса.Добавить("/DumpCfg")
    параметрыПроцесса.Добавить(путьКФайлуCF)

    возврат ВыполнитьКомандуСистемы(имяФайлаПлатформы, параметрыПроцесса)
;

метод E1c_CreateDistributionFiles(имяФайлаПлатформы: Строка, путьКИБ: Строка, путьКФайлуCF: Строка): Число
    пер параметрыПроцесса = новый Массив()
    параметрыПроцесса.Добавить("DESIGNER")
    параметрыПроцесса.Добавить("/F")
    параметрыПроцесса.Добавить(путьКИБ)
    параметрыПроцесса.Добавить("/CreateDistributionFiles")
    параметрыПроцесса.Добавить("-cffile")
    параметрыПроцесса.Добавить(путьКФайлуCF)

    возврат ВыполнитьКомандуСистемы(имяФайлаПлатформы, параметрыПроцесса)
;

метод МаксимальныеПлатформы(): Соответствие
    знч потокВывода = ВыполнитьСкрипт("../Библиотеки/УстановленныеПлатформы83.sbsl", "МаксимальныеПлатформы", [])
    знч максимальныеПлатформы = ОбъектИзПотокаВывода(потокВывода) как Соответствие

    возврат максимальныеПлатформы
;

метод КодировкаПотокаВывода(): Строка
    возврат "Cp1251"
;

метод ВыполнитьКомандуСистемы(имяКоманды: Строка, параметры: Массив, выводитьОшибки: Булево = Истина): Число
    если ОТЛАДКА
        Консоль.Записать(имяКоманды + " " + Строки.Соединить(параметры, " "))
    ;

    знч процесс = новый ПроцессОс(имяКоманды, параметры, Ложь)
    процесс.Запустить()
    процесс.ОжидатьЗавершения()

    знч кодВозврата = процесс.ПолучитьКодВозврата()
    если кодВозврата != 0
        Консоль.Записать("Код возврата:" + кодВозврата)
    ;

    пер результатВыполненияСкрипта = процесс.ПолучитьПотокВывода()
    знч текстРезультата = результатВыполненияСкрипта.ПрочитатьКакТекст(КодировкаПотокаВывода())
    если не текстРезультата.Пусто()
        Консоль.Записать(текстРезультата)
    ;

    если кодВозврата == 0 или не выводитьОшибки
        возврат 0
    ;

    пер ошибкиВыполненияСкрипта = процесс.ПолучитьПотокОшибок()
    знч текстОшибок = ошибкиВыполненияСкрипта.ПрочитатьКакТекст(КодировкаПотокаВывода())
    если не текстОшибок.Пусто()
        Консоль.Записать("Ошибки при выполнении процесса:")
        Консоль.Записать(текстОшибок)
    ;

    возврат кодВозврата
;


перечисление ОперационныеСистемы
    Windows,
    MacOS,
    Linux
;


метод ИспользуемаяОС(): ОперационныеСистемы
    знч имяОС = СредаИсполнения.ПолучитьСвойство("os.name")

    выбор когда имяОС.НачинаетсяС("windows", Истина)
        возврат ОперационныеСистемы.Windows

    когда имяОС.Содержит("mac", Истина)
        возврат ОперационныеСистемы.MacOS

    когда имяОС.Содержит("nux", Истина)
        возврат ОперационныеСистемы.Linux

    иначе
        выбросить новый ИсключениеНедопустимоеСостояние("Неизвестная ОС")
    ;
;

/* 
 * Методы для вызова других скриптов. Необходимо копировать в свой скрипт
 */
метод ВыполнитьСкрипт(имяФайлаСкрипта: Строка, имяКомандыСкрипта: Строка, параметрыСкрипта: Массив): ПотокЧтения
    пер расширениеИсполнителя: Строка
    знч имяОС = СредаИсполнения.ПолучитьСвойство("os.name")
    выбор когда имяОС.НачинаетсяС("windows", Истина)
        расширениеИсполнителя = "cmd"
    иначе
        расширениеИсполнителя = "sh"
    ;
    знч путьКИсполнителю = СредаИсполнения.ПолучитьСвойство("logback.configurationFile").Удалить("config" + Файлы
        .СимволРазделителя + "logback.xml") + "bin/executor_j11." + расширениеИсполнителя

    знч командаТекущегоСкрипта = СредаИсполнения.ПолучитьСвойство("sun.java.command")
    пер имяФайлаТекущегоСкрипта = командаТекущегоСкрипта.Подстрока(0, командаТекущегоСкрипта.Найти(".sbsl") + 5)
    имяФайлаТекущегоСкрипта = имяФайлаТекущегоСкрипта.Подстрока(имяФайлаТекущегоСкрипта.Найти(" ") + 1)
    имяФайлаТекущегоСкрипта = имяФайлаТекущегоСкрипта.Подстрока(имяФайлаТекущегоСкрипта.Найти(" ") + 1)
    знч файлТекущегоСкрипта = новый Файл(имяФайлаТекущегоСкрипта)
    пер путьТекущегоСкрипта = ""
    если файлТекущегоСкрипта.Каталог != Неопределено
        путьТекущегоСкрипта = файлТекущегоСкрипта.Каталог.Путь + Файлы.СимволРазделителя
    ;

    пер аргументыПроцессаОс = ["-s", путьТекущегоСкрипта + имяФайлаСкрипта, "-m", имяКомандыСкрипта]
    если не параметрыСкрипта.Пусто()
        аргументыПроцессаОс.ДобавитьВсе(параметрыСкрипта)
    ;

    знч библиотека = новый ПроцессОс(путьКИсполнителю, аргументыПроцессаОс, Ложь)

    библиотека.Запустить()
    библиотека.ОжидатьЗавершения()

    знч ошибкиВыполненияСкрипта = библиотека.ПолучитьПотокОшибок()
    знч текстОшибок = ошибкиВыполненияСкрипта.ПрочитатьКакТекст(КодировкаПотокаВыводаСкриптов())
    если не текстОшибок.Пусто()
        выбросить новый ИсключениеНедопустимоеСостояние(текстОшибок)
    ;

    возврат библиотека.ПотокВывода
;

метод КодировкаПотокаВыводаСкриптов(): Строка
    возврат СредаИсполнения.ПолучитьСвойство("file.encoding")
;

метод НастройкиПотокаВыводаСкриптов(): НастройкиЧтенияДанных
    пер настройкиПотокаВывода = новый НастройкиЧтенияДанных()
    настройкиПотокаВывода.Кодировка = КодировкаПотокаВыводаСкриптов()

    возврат настройкиПотокаВывода
;

метод ОбъектИзПотокаВывода(потокВывода: ПотокЧтения): Строка|Массив|Соответствие
    пер массивСтрок: Массив

    знч результатВыполненияСкрипта = новый ЧтениеДанных(потокВывода, НастройкиПотокаВыводаСкриптов())
    пока не результатВыполненияСкрипта.ЧтениеЗавершено()
        знч прочитаннаяСтрока = результатВыполненияСкрипта.ПрочитатьСтроку()

        если прочитаннаяСтрока.Пусто()
        продолжить
        ;

        выбор прочитаннаяСтрока[0]
        когда "["
            знч результатМассивом = МассивИзСтроки(прочитаннаяСтрока)
            если результатМассивом.Размер() == 1
                массивСтрок.Добавить(результатМассивом[0])
            иначе
                возврат результатМассивом
        ;
        когда "{"
            возврат СоответствиеИзСтроки(прочитаннаяСтрока)

        иначе
            массивСтрок.Добавить(прочитаннаяСтрока)
        ;
    ;
    возврат массивСтрок
;

метод МассивИзСтроки(строкаПотокаВывода: Строка): Массив
    знч результатСтрокой = строкаПотокаВывода.Подстрока(1, строкаПотокаВывода.Длина() - 1)
    знч результатМассивом = результатСтрокой.Разделить(", ", Ложь)
    возврат результатМассивом
;

метод СоответствиеИзСтроки(строкаПотокаВывода: Строка): Соответствие
    пер результатСоответствием: Соответствие

    знч результатСтрокой = строкаПотокаВывода.Подстрока(1, строкаПотокаВывода.Длина() - 1)

    знч результатМассивом = результатСтрокой.Разделить(", ")
    для строкаРезультата из результатМассивом
        знч ключИЗначение = строкаРезультата.Разделить("=")

        результатСоответствием.Вставить(ключИЗначение[0], ключИЗначение[1])
    ;

    возврат результатСоответствием
;