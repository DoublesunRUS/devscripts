#!/usr/bin/executor

/*******************************************************************************
 * Copyright (c) 2020 Alexander Kapralov and Contributors
 * This program and the accompanying materials are made available under
 * the terms of the BSD 3-Clause License which is available at
 * https://spdx.org/licenses/BSD-3-Clause.html#licenseText
 *
 * SPDX-License-Identifier: BSD-3-Clause
 *
 * Contributors:
 *    
 *
 ******************************************************************************/

структура ОписаниеНастроек
	пер ИмяФайлаШаблона: Строка
	пер КаталогФич: Строка
	пер ИмяПеременнойОбъекта: Строка
	пер Объекты: Массив
	пер Пользователи: Массив
;

структура ОписаниеПользователя
	пер Представление: Строка
	пер Имя: Строка
	пер Пароль: Строка
;


метод Скрипт(путьКФайлам: Строка)
	пер списокНастроек = новый Массив()

	пер настройкиПоиска = новый НастройкиПоискаФайлов()
	настройкиПоиска.ИмяСодержит(".json")
	для файлНастроек из Файлы.Найти(путьКФайлам, настройкиПоиска)
		исп потокФайлаНастроек = файлНастроек.ОткрытьПотокЧтения()

		пер настройкиJson = новый НастройкиЧтенияОбъектовJson()
		настройкиJson.ИгнорироватьНеизвестныеСвойства = Истина

		пер настройки = СериализацияJson.ПрочитатьОбъект(потокФайлаНастроек, Тип(ОписаниеНастроек), настройкиJson) как ОписаниеНастроек
		пер пользователи = новый Массив()
		для пользователь из настройки.Пользователи
			знч описаниеПользователя = новый ОписаниеПользователя(пользователь["ПредставлениеПользователя"], пользователь["ИмяПользователя"],
				пользователь["ПарольПользователя"])
			пользователи.Добавить(описаниеПользователя)
		;
		настройки.Пользователи = пользователи

		списокНастроек.Добавить(настройки)
	;

	для настройки из списокНастроек
		пер файлШаблона = новый Файл(настройки.ИмяФайлаШаблона)
		исп потокШаблона = файлШаблона.ОткрытьПотокЧтения()
		знч текстШаблона = потокШаблона.ПрочитатьКакТекст()
		пер имяФайлаБезРасширения = файлШаблона.ИмяБезРасширения
		для объект из настройки.Объекты
			для пользователь из настройки.Пользователи
				пер текстФичи = текстШаблона
				текстФичи = текстФичи.Заменить("\%%{настройки.ИмяПеременнойОбъекта}\%", объект)
				текстФичи = текстФичи.Заменить("\%ПредставлениеПользователя\%", пользователь.Представление)
				текстФичи = текстФичи.Заменить("\%ИмяПользователя\%", пользователь.Имя)
				текстФичи = текстФичи.Заменить("\%ПарольПользователя\%", пользователь.Пароль)
				
				пер файлФичи = новый Файл("%{настройки.КаталогФич}/%имяФайлаБезРасширения-%объект-%{пользователь.Представление}.feature")
				исп потокФичи = файлФичи.ОткрытьПотокЗаписи()
				потокФичи.Записать(текстФичи)
			;
		;
	;
;